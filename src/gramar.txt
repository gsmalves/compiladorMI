! ===================================================================
! Gramatica Desenvolvida para disciplina 
! EXA869 - MI Processadores de Linguagem de Programação
! ===================================================================


"Case Sensitive" = True
"Start Symbol"   = <Program>


! ------------------------------------------------- Terminals

Identifier    = {Letter}({AlphaNumeric} | '_')*
{String Chars} = {Printable} + {HT} - ["\]
StringLiteral = '"' ( {String Chars} | '\' {Printable} )* '"'
{Hex Digit}      = {Digit} + [abcdefABCDEF]
{Oct Digit}      = [01234567]
DecLiteral       = [123456789]{digit}*
OctLiteral       = 0{Oct Digit}*
HexLiteral       = 0x{Hex Digit}+
FloatLiteral     = {Digit}*'.'{Digit}+

<Number> ::= DecLiteral 
          | OctLiteral 
          | HexLiteral 
          | FloatLiteral
<PrefixGlobalLocal> ::= 'global.' | 'local.' 

! ===================================================================
! Comments
! ===================================================================

Comment Start = '/*'
Comment End   = '*/'
Comment Line  = '//'


! ===================================================================
! Start 
! ===================================================================

<Program> ::= <Global Decl><Decls><Start> 
<Global Decl> ::= <Const Decl> 
                | <Var Decl> 
                | <Const Decl><Var Decl>  
                | <Var Decl><Const Decl>               
                |

<Start> ::= 'start' '(' ')' '{' <Body Procedure> '}' <Decls> 
        
<Decls> ::= <Decl> <Decls> | 
          

<Decl>  ::= <Function Declaration>
          | <Proc Decl>
          | <Struct Decl>
          | <Typedef Decl>

             
! ===================================================================
! Paramters List
! ===================================================================
<Formal Parameter List> ::= <Exp> 
                         | <Exp> ',' <Formal Parameter List>
                         |
      

<Formal Parameter List Read> ::=  Identifier 
                              | <Formal Parameter List Read> ',' Identifier

             
! ===================================================================
! Function  Declaration
! ==================================================================


<Function Declaration> ::= 'function' <Type> Identifier '(' <Params> ')' '{' <Body> '}'

! ===================================================================
! Procedure  Declaration
! ===================================================================               

<Proc Decl> ::= 'procedure' Identifier '(' <Params> ')' '{' <Body Procedure> '}'
              | Identifier '(' <Formal Parameter List> ')' '{' <Body Procedure> '}'

<Params>     ::= <Param> ',' <Params>
               | <Param> | 
               
<Param>      ::= const <Type> Identifier
               |       <Type> Identifier
               
! ===================================================================
! Function n Procedures call
! ===================================================================

<Function Call> ::= Identifier '(' <Formal Parameter List> ')'

! ===================================================================
! Type Declaration
! ===================================================================

<Typedef Decl> ::= typedef <Base> Identifier ';'

<Base> ::= <Type>

        | struct <Extends> '{' <VariablesList> '}'
        | <Struct Decl>  

<Struct Decl>  ::= struct Identifier <Extends> '{' <VariablesList> '}'  
                   
<Extends> ::= 'extends' Identifier |

! ===================================================================
! Constant Declaration
! =================================================================== 
<Const Decl> ::= 'const' '{' <ConstList> '}' 
              
<ConstList> ::= <Type> <Const> <ConstList> |  

<Const> ::= Identifier '=' <Value> <Delimiter Const> 
                                                                    
<Delimiter Const> ::= ',' <Const> |';'
                                                                                  
<Type> ::= int 
        | real 
        | boolean 
        | string
        | struct Identifier 
        | Identifier

<Value> ::= <Number>
         | <Boolean Literal> 
         | StringLiteral
                               
! ===================================================================
! Variable Declaration
! ==================================================================

<Var Decl> ::= 'var' '{' <VariablesList> '}'         
<VariablesList> ::= <Type> <Variable> <VariablesList> |  
<Variable> ::= Identifier<Aux>  
<Aux> ::= '=' <Value> <Delimiter Var> | <Delimiter Var>|  <Vector><Assignment_vector><Delimiter Var>  | <Matrix><Assignment_matrix><Delimiter Var>                                                   
<Delimiter Var> ::= ',' <Variable> |';'
                 
<Index> ::= DecLiteral|OctLiteral| Identifier    
<Vector> ::= '['<Index>']'     
<Matrix> ::= '['<Index>']' <Vector>  
          
<Assignment_vector> ::= <Assignment_vector_aux1> | <Assignment_vector_aux2> |
<Assignment_vector_aux1> ::= '=' <Value>
<Assignment_vector_aux2> ::= '=' '{'<Value_assigned_vector>'}'
<Value_assigned_vector> ::= <Value> ',' <Value_assigned_vector> | <Value>

<Assignment_matrix> ::= <Assignment_matrix_aux1> | <Assignment_matrix_aux2> |
<Assignment_matrix_aux1> ::=  <Assignment_vector_aux1>      
<assignment_matrix_aux2> ::=  '=' '{' '{' <Value_assigned_matrix> '}' <Dimensao_matrix2>
<Dimensao_matrix2> ::= ',' '{'<Value_assigned_matrix> '}' '}'
<Value_assigned_matrix> ::= <Value> ',' <Value_assigned_matrix> | <Value> 


! ===================================================================
! While
! ===================================================================

<While> ::= 'while' '(' <Conditional Expression> ')' '{' <Body> '}'
         
<While Procedure> ::= 'while' '(' <Conditional Expression> ')' '{' <Body Procedure> '}'
         
<Boolean Literal> ::= 'true'
                    | 'false'

<Conditional Expression> ::= <Boolean Literal> 
                            | <Relational Expression>
                            | <Logical Expression>
                            
! ===================================================================
! if - then - else
! =================================================================== 

<If> ::= 'if' '(' <Conditional Expression><Then> 
<Then> ::= ')' 'then' '{'<Body>'}' <Else>
<Else> ::= 'else' '{' <Body> '}' | 
        
<If Procedure> ::= 'if' '(' <Conditional Expression><Then Procedure> 
<Then Procedure> ::= ')' 'then' '{'<Body Procedure>'}' <Else Procedure>
<Else Procedure> ::= 'else' '{' <Body Procedure> '}' | 

! ===================================================================
! read
! ===================================================================
<Read>  ::= read'(' <Formal Parameter List Read> ')' ';'

! ===================================================================
! print
! ===================================================================

<Print> ::= print'(' <Formal Parameter List>  ')' ';'

! ===================================================================
! Statements
! ===================================================================
<Body>        ::=  <Body Item><Body> 
                 |  
              
<Body Item>  ::=  <Var Decl>
                 | <While>
                 | <If>
                 | <Read>
                 | <Print>
                 | <Assign>
                 | <Return Statement>

<Return Statement> ::= 'return' ';'
                       | 'return' <Assign>
                       
<Body Procedure>  ::=  <Body Item Procedure><Body Procedure> 
                 |  
              
<Body Item Procedure>  ::=  <Var Decl>
                 | <While Procedure>
                 | <If Procedure>
                 | <Read>
                 | <Print>
                 | <Assign>
                 
! ===================================================================
! Logical Expressions 
! ===================================================================

<Conditional Operator>  ::= '&&'
                      | '||' 

<Logical Denied> ::= '!' Identifier 
                  | '!' <Boolean Literal>
                  | '!' <Logical Expression>
                  | '!' <Relational Expression>
                  
<Logical> ::= <Conditional Operator> <Expression Value Logical> | <Conditional Operator> <Logical Denied>
           
<Expression Value Logical>  ::= Identifier       
                      | <Boolean Literal> 
                      | StringLiteral
                      | <Function Call>
                      | <Relational Expression>

<Logical Expression> ::= <Expression Value Logical> <Logical> | <Logical Denied>   
                      
! ===================================================================
! Arithmetic Expressions 
! ===================================================================
<Relational Expression>  ::= <Exp> <Relational>   
               
<Relational> ::= '>'  <Exp> 
               |  '<'  <Exp> 
               |  '<=' <Exp> 
               |  '>=' <Exp>
               |  '==' <Exp>    !Equal
               |  '!=' <Exp>    !Not equal


<Assign> ::= <PrefixGlobalLocal> Identifier '=' <Exp> ';' 
           | Identifier '=' <Exp> ';'
           | Identifier <Vector><Assignment_vector> ';' | Identifier <Matrix><Assignment_matrix> ';' | <Exp> ';' 
              
<Exp> ::= <PrefixGlobalLocal> <Term> <Add Exp> | <Term> <Add Exp>
                      
<Add Exp>     ::=  '+'  <Exp>
               |   '-'  <Exp> 
               | 

<Term> ::= <Expression Value> <Mult Exp>

<Mult Exp>    ::= '*' <Term> 
               |  '/' <Term>  
               |

<Expression Value>  ::= '-' <Expression Value> 
               |  Identifier
               | '(' <Exp> ')'
               | <Number>        
               | <Boolean Literal> 
               | StringLiteral
               | <Function Call>